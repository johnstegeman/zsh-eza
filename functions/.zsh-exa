# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#
# Copyright (c) 2022 Salvydas Lukosius

if [[ $TERM == 'dumb' ]]; then
  return 1
fi

builtin emulate -L zsh ${=${options[xtrace]:#off}:+-o xtrace}
builtin setopt extended_glob warn_create_global typeset_silent

if (( $+commands[eza] )); then
  typeset enable_autocd=0
  typeset -ag exa_params

  exa_params=(
    '--git' '--icons' '--classify' '--group'
    '--group-directories-first' '--time-style=long-iso' '--color-scale'
  )

  alias ls='eza $exa_params'
  alias l='eza --git-ignore $exa_params'
  alias ll='eza --all --header --long $exa_params'
  alias llm='eza --all --header --long --sort=modified $exa_params'
  alias la='eza -lbhHigUmuSa'
  alias lx='eza -lbhHigUmuSa@'
  alias lt='eza --tree $exa_params'
  alias tree='eza --tree $exa_params'

  [[ "$AUTOCD" = <-> ]] && enable_autocd="$AUTOCD"
  if [[ "$enable_autocd" == "1" ]]; then
    # Function for cd auto list directories
    →auto-exa() { command eza $exa_params; }
    [[ $chpwd_functions[(r)→auto-exa] == →auto-exa ]] || chpwd_functions=( →auto-exa $chpwd_functions )
  fi
else
  print "Please install eza before using this plugin." >&2
  return 1
fi

return 0
